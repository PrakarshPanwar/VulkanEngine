#version 460 core
#extension GL_EXT_ray_tracing : require

layout(binding = 0) uniform accelerationStructureEXT u_TopLevelAS;
layout(binding = 1) writeonly uniform image2D o_Image;

layout(binding = 2) uniform Camera
{
	mat4 Projection;
	mat4 View;
    mat4 InverseProjection;
	mat4 InverseView;
} u_Camera;

struct RayPayload
{
	vec3 Color;
};

layout(location = 0) rayPayloadEXT RayPayload o_RayPayload;

void main()
{
	const vec2 texCoords = (gl_LaunchIDEXT.xy + vec2(0.5)) / gl_LaunchSizeEXT.xy;
	vec2 d = texCoords * 2.0 - 1.0;

	vec4 origin = u_Camera.InverseView * vec4(0.0, 0.0, 0.0, 1.0);
	vec4 target = u_Camera.InverseProjection * vec4(d.x, d.y, 1.0, 1.0);
	vec4 direction = u_Camera.InverseView * vec4(normalize(target.xyz), 0.0);

	float tMin = 0.1;
	float tMax = 1000.0;

	traceRayEXT(u_TopLevelAS,
		gl_RayFlagsOpaqueEXT,
		0xFF,
		0,
		0,
		0,
		origin.xyz,
		tMin,
		direction.xyz,
		tMax,
		0);

	imageStore(o_Image, ivec2(gl_LaunchIDEXT.xy), vec4(o_RayPayload.Color, 1.0));
}
